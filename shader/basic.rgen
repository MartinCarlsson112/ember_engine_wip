#version 460
#extension GL_EXT_ray_tracing : require
layout(binding = 0, set = 0) uniform accelerationStructureEXT tlas;
layout(binding = 1, set = 0, rgba8) uniform image2D image; 
layout(binding = 2, set = 0) uniform camera_data
{
	mat4 perspective;
    mat4 view;
    mat4 view_inverse;
    mat4 proj_inverse;
    vec4 cam_pos;
} cam;

layout(location = 0) rayPayloadEXT vec3 hit_value;

void main()
{
	const vec2 pixel_center = vec2(gl_LaunchIDEXT.xy) + vec2(0.5);
	const vec2 in_uv = pixel_center/vec2(gl_LaunchSizeEXT.xy);
	vec2 d = in_uv * 2.0 - 1.0;

	vec4 origin = cam.view_inverse * vec4(0,0,0,1);
	vec4 target = cam.proj_inverse * vec4(d.x, d.y, 1, 1) ;
	vec4 direction = cam.view_inverse* vec4(normalize(target.xyz / target.w), 0) ;
	float tmin = 0.001;
	float tmax = 10000.0;
	traceRayEXT(tlas, gl_RayFlagsOpaqueEXT, 0xff, 0, 0, 0, origin.xyz, tmin, direction.xyz, tmax, 0);
	imageStore(image, ivec2(gl_LaunchIDEXT.xy), vec4(hit_value, 1.0));
}
